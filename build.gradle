buildscript {
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.0-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
    }
}

apply plugin: 'net.minecraftforge.gradle.tweaker-client'
apply plugin: 'checkstyle'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'

ext {
    // Artefact details
    buildNumber = project.hasProperty("buildNumber") ? buildNumber : '0'
    ciSystem = project.hasProperty("ciSystem") ? ciSystem : 'unknown'
    commit = project.hasProperty("commit") ? commit : 'unknown'
    classifier = project.hasProperty("buildType") ? buildType : 'SNAPSHOT'
    isReleaseBuild = "RELEASE".equals(project.classifier.toUpperCase())
    mavenRepo = project.isReleaseBuild ? "mavenUrl" : "mavenSnapshotUrl"
    
    // Extended project information
    projectName = 'LiteLoader'
    inceptionYear = '2012'
    packaging = 'jar'
    
    startClass = 'com.mumfrey.liteloader.debug.Start'
    tweakClass = 'com.mumfrey.liteloader.launch.LiteLoaderTweaker'

    mixinSrg = new File(project.buildDir, 'tmp/mixins.liteloader/mixins.srg')
    mixinRefMap = new File(project.buildDir, "tmp/mixins.liteloader/mixins.liteloader.refmap.json")
}

// Basic project information
group = "com.mumfrey"
archivesBaseName = "liteloader"
version = buildVersion + (project.isReleaseBuild ? '' : '-' + project.classifier)

// Minimum version of Java required
sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories {
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven/'
    }
}

dependencies {
    compile('org.spongepowered:mixin:0.4.7-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
    }
}

minecraft {
    version = project.mcVersion
    mappings = project.mcMappings
    runDir = "run"
    tweakClass = project.tweakClass
}

sourceSets {
    client {
        compileClasspath += main.compileClasspath + main.output
    }
    debug {
        compileClasspath += client.compileClasspath + client.output
    }
}

checkstyle {
    configProperties = [
            "name"        : project.name,
            "organization": project.organization,
            "url"         : project.url,
            "year"        : project.inceptionYear
    ]
    configFile = file("checkstyle.xml")
    toolVersion = '6.13'
}

javadoc {
    source sourceSets.client.allJava
    source sourceSets.debug.allJava
}

// hacks for run configs
afterEvaluate {
    def mc = plugins.getPlugin 'net.minecraftforge.gradle.tweaker-client'
    mc.replacer.putReplacement '{RUN_CLIENT_MAIN}', project.startClass
    mc.replacer.putReplacement '{RUN_CLIENT_TWEAKER}', minecraft.tweakClass
}

// manifest entries for all jars
def jarManifest = {
    mainAttributes (
        'Built-By': System.properties['user.name'],
        'Created-By': System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
        'Implementation-Title': name,
        'Implementation-Version': version + "+" + ciSystem + "-b" + buildNumber + ".git-" + commit,
        'Implementation-Vendor': url
    )
}

jar {
    from sourceSets.client.output
    from sourceSets.debug.output
    manifest jarManifest
}

task releaseJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.client.output
    from project.mixinRefMap
    
    manifest jarManifest
    classifier = 'staging'
}

shadowJar {
    manifest jarManifest
    dependsOn 'reobfReleaseJar'

    from sourceSets.main.output
    from sourceSets.client.output
    from project.mixinRefMap
    
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'dummyThing'
    exclude 'LICENSE.txt'
    
    dependencies {
        include(dependency('org.spongepowered:mixin'))
    }
    
    classifier = 'release'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

// Hey @AbrarSyed why can't we just turn this off >:(
task runClient(type: JavaExec, overwrite: true) {
    doFirst {
        println "Do not use runClient, it is not compatible with Mixin"
        System.exit(-1)
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += [
        '-Xlint:all',
        '-Xlint:-path',
        '-Xlint:-rawtypes',
        '-Xlint:-processing',
        "-AoutSrgFile=${project.mixinSrg.canonicalPath}",
        "-AoutRefMapFile=${project.mixinRefMap.canonicalPath}",
        "-AreobfSrgFile=${genSrgs.mcpToNotch.canonicalPath}"
    ]
    options.deprecation = true
    options.encoding = 'utf8'
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        // disable the crazy super-strict doclint tool in Java 8
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

reobf {
    jar {
        useSrgSrg()
    }
    releaseJar {
        useNotchSrg()
        classpath = sourceSets.main.compileClasspath
        extraFiles([
            project.mixinSrg
        ])
    }
    shadowJar {
        useNotchSrg()
        classpath = sourceSets.main.compileClasspath
        extraFiles([
            project.mixinSrg
        ])
    }
}

compileJava.doFirst {
    println '------------------------------------------------------------'
    println 'Running ' + (project.isReleaseBuild ? "RELEASE" : "SNAPSHOT") + ' build'
    println '------------------------------------------------------------'
}

build.dependsOn {[
    'reobfReleaseJar',
    'reobfShadowJar'
    ]
}

artifacts {
    if (project.isReleaseBuild) {
        archives jar
    }
    archives shadowJar
    archives sourceJar
    archives javadocJar
}

task deploy(type: Copy, dependsOn: build) {
    def libraryDir = new File(new File(System.env.APPDATA), ".minecraft/libraries")
    from shadowJar.outputs.files[0]
    into new File(libraryDir, sprintf('%1$s%4$s%2$s%4$s%3$s', project.group.replace('.', File.separator), archivesBaseName, buildVersion, File.separatorChar)) 
    rename shadowJar.outputs.files[0].name, sprintf("%s-%s.jar", archivesBaseName, buildVersion)
}

uploadArchives { 
    repositories {
        mavenDeployer {
            if (project.hasProperty(project.mavenRepo)) {
                repository(url: project.getProperty(project.mavenRepo)) {
                    authentication(userName: project.mavenUsername, password: project.mavenPassword)
                }
            }
            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'LiteLoader'
                    url 'http://www.liteloader.com/'
                    scm {
                        url 'http://develop.liteloader.com/liteloader/LiteLoader'
                        connection 'scm:git:http://develop.liteloader.com/liteloader/LiteLoader.git'
                        developerConnection 'scm:git:http://develop.liteloader.com/liteloader/LiteLoader.git'
                    }
                    issueManagement {
                        system 'GitLab Issues'
                        url 'http://develop.liteloader.com/liteloader/LiteLoader/issues'
                    }
                }
            }
        }
    }
}
